# Maintainer: phantop <phantop@tuta.io>
pkgname=dwarfs-universal
pkgver=0.9.10
pkgrel=1
pkgdesc='A fast high compression read-only file system (pre-compiled binaries)'
url='https://github.com/mhx/dwarfs'
source=("https://github.com/mhx/dwarfs/releases/download/v${pkgver}/dwarfs-${pkgver}-Linux-${CARCH}-clang.tar.xz"
  "https://github.com/mhx/dwarfs/releases/download/v${pkgver}/dwarfs-universal-${pkgver}-Linux-${CARCH}-clang"
  "https://github.com/mhx/dwarfs/raw/main/scripts/extract_blocks.py")
arch=('x86_64' 'aarch64')
license=('GPL3')
depends=( 'fuse3' )
conflicts=('dwarfs')
provides=('dwarfs')

package() {
  install -Dm755 "${srcdir}/dwarfs-universal-${pkgver}-Linux-${CARCH}-clang" "${pkgdir}/usr/bin/dwarfsuniversal"
  install -Dm755 "${srcdir}/extract_blocks.py" "${pkgdir}/usr/bin/dwarfsblocks"
  ln -s dwarfsuniversal "${pkgdir}/usr/bin/dwarfsck"
  ln -s dwarfsuniversal "${pkgdir}/usr/bin/dwarfsextract"
  ln -s dwarfsuniversal "${pkgdir}/usr/bin/mkdwarfs"
  ln -s dwarfsuniversal "${pkgdir}/usr/bin/dwarfs"

  # mount will just run dwarfsuniversal otherwise
  printf '#!/bin/sh\ndwarfs "$@"' > "${pkgdir}/usr/bin/mount.dwarfs"
  chmod 755 "${pkgdir}/usr/bin/mount.dwarfs"

  cd "${srcdir}/dwarfs-${pkgver}-Linux-${CARCH}-clang"
  install -Dm644 share/man/man1/dwarfs.1 "$pkgdir/usr/share/man/man1/dwarfs.1"
  install -Dm644 share/man/man1/dwarfsck.1 "$pkgdir/usr/share/man/man1/dwarfsck.1"
  install -Dm644 share/man/man1/dwarfsextract.1 "$pkgdir/usr/share/man/man1/dwarfsextract.1"
  install -Dm644 share/man/man1/mkdwarfs.1 "$pkgdir/usr/share/man/man1/mkdwarfs.1"
  install -Dm644 share/man/man5/dwarfs-format.5 "$pkgdir/usr/share/man/man5/dwarfs-format.5"
}
# update by removing and running `makepkg -g >> PKGBUILD`
sha256sums=('84581c5e96e6c6c88a4b84607aafe72618e323ef0116ee62ef7f1b67fe17cb58'
            'deb1ac4ad685ced198fb853c5693506577ea9d9898b0751a4058d05133be0e46'
            'd5367816de9b102b0d5cb3fce0b110c29c37c1edc725b5044677f2d90c3bf95a')
